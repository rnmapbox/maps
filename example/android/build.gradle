buildscript {
    ext {
        RNMapboxMapsImpl = "mapbox"

        useCustomMapbox = false

        if (System.getenv('CI_MAP_IMPL').equals('mapbox')) {
            RNMapboxMapsImpl = "mapbox"
            RNMapboxMapsVersion = '10.19.0'
            kotlinVersion = '1.6.21'
        } else if (System.getenv('CI_MAP_IMPL').equals('mapbox11')) {
            RNMapboxMapsImpl = "mapbox"
        } else if (project.hasProperty('RNMBX11')) {
            if (project.getProperty('RNMBX11').toBoolean()) {
                // RNMapboxMapsVersion = '11.15.2' - default
            } else {
                RNMapboxMapsVersion = '10.19.0'
            }
        }

        // Mapbox deps
        if (useCustomMapbox) {
            RNMapboxMapsLibs = {
                implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.7.1'
                implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-gestures:0.7.0'

                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.14.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0'
            }
        }

        buildToolsVersion = "36.0.0"
        minSdkVersion = 24
        compileSdkVersion = 36
        targetSdkVersion = 36
        kotlinVersion = "2.1.20"
        ndkVersion = "27.1.12297006"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = 'mapbox'
                // Use the secret token you stored in gradle.properties as the password
                password = project.properties['MAPBOX_DOWNLOADS_TOKEN'] ?: ""
            }
        }
    }
}

apply plugin: "com.facebook.react.rootproject"
