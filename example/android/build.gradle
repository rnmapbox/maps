// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 21
        compileSdkVersion = 30
        targetSdkVersion = 30
        ndkVersion = "21.4.7075529"

        useMapLibre = false
        useCustomMapbox = false

        // Mapbox deps
        if (useCustomMapbox) {
            rnmbglMapboxLibs = {
                implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.7.1'
                implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-gestures:0.7.0'
            }

            rnmbglMapboxPlugins = {
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.14.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0'
            }
        }


        // MapLibre deps
        if (useMapLibre) {
            rnmbglMapboxLibs = {
                implementation ("org.maplibre.gl:android-sdk:9.2.1")
                implementation ("com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0")
                implementation 'com.mapbox.mapboxsdk:mapbox-android-telemetry:6.1.0'
            }

            rnmbglMapboxPlugins = {
                implementation ("com.mapbox.mapboxsdk:mapbox-android-gestures:0.7.0")
                implementation ("com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.14.0")    {
                    exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
                }
                implementation ("com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0")        {
                    exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
                }
                implementation ("com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0") {
                    exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
                }
            }
        }
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.1.0")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        if (rootProject.ext.get('useCustomMapbox')) {
                maven {
                    url 'https://api.mapbox.com/downloads/v2/releases/maven'
                    authentication {
                        basic(BasicAuthentication)
                    }
                    credentials {
                    username = "mapbox"
                    password = "REPLACE WITH YOUR MAPBOX DOWNLOAD TOKEN"

                    }
                }
        }


        if (rootProject.ext.get('useMapLibre')) {
            maven {
                url = "https://dl.bintray.com/maplibre/maplibre-gl-native"
            }
        }

        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}
