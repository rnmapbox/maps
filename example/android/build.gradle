// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        RNMapboxMapsImpl = "mapbox"

        if (project.hasProperty('RNMBX11') && project.getProperty('RNMBX11').toBoolean()) {
            RNMapboxMapsUseV11 = true
            RNMapboxMapsVersion = '11.0.0-beta.3'
        }

        useCustomMapbox = false

        if (System.getenv('CI_MAP_IMPL').equals('mapbox')) {
            RNMapboxMapsImpl = "mapbox"
            kotlinVersion = '1.6.21'
        } else if (System.getenv('CI_MAP_IMPL').equals('mapbox11')) {
            RNMapboxMapsUseV11 = true
            RNMapboxMapsVersion = '11.0.0-beta.3'
            RNMapboxMapsImpl = "mapbox"
        }

        // Mapbox deps
        if (useCustomMapbox) {
            RNMapboxMapsLibs = {
                implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.7.1'
                implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-gestures:0.7.0'

                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.14.0'
                implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0'
            }
        }

        kotlinVersion = '1.8.0'
        buildToolsVersion = "33.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 33

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = "mapbox"
                // Use the secret token you stored in gradle.properties as the password
                password = MAPBOX_DOWNLOADS_TOKEN
            }
        }
    }
}